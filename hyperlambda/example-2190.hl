
// Returns a list of IDs from NetSuite to caller according to arguments
.arguments

   // Mandatory argument declaring the type of entities we want to list.
   type:string

   // Optional filtering for contacts to search for.
   q:string

   // Optional limit for how many records to select. Defaults to 1000 if not specified.
   limit:long

   // Optional offset of where to start returning records. Defaults to 0 if not specified.
   offset:long
.type:public

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/type

/*
 * Returns an access token for NetSuite.
 * 
 * Returns a JWT token that can be used to invoke NetSuite according to your configuration settings.
 * Notice, this will read account-id, certificate-id, and consumer-key from your configuration settings,
 * in addition to assume your private key exists in the file '/etc/netsuite/private-key.txt'.
 */
execute:magic.workflows.actions.execute
   name:netsuite-get-access-token
   filename:/modules/netsuite/workflows/actions/netsuite-get-access-token.hl
   arguments

// Creating our idempotency key to prevent replay attacks.
guid.new

// Creating invocation URL.
.url
set-value:x:@.url
   strings.concat
      .:"https://"
      config.get:"magic:netsuite:account-id"
      .:.suitetalk.api.netsuite.com/services/rest/record/v1
      .:/
      get-value:x:@.arguments/*/type

// Removing type to avoid unsupported query parameter.
remove-nodes:x:@.arguments/*/type

// Invoking our HTTP GET endpoint, now correctly parametrised.
http.get:x:@.url
   token:x:--/execute/=netsuite-get-access-token/*/access-token
   headers
      X-NetSuite-Idempotency-Key:x:@guid.new
   query:x:@.arguments/*

// Converts the result to a lambda object.
json2lambda:x:@http.get/*/content

// Sanity checking above invocation.
if
   not
      and
         mte:x:@http.get
            .:int:200
         lt:x:@http.get
            .:int:300
   .lambda

      // Oops, error ...!!
      lambda2hyper:x:@json2lambda
      log.error:x:@lambda2hyper
      throw:Could not list entities from NetSuite, check your log for details.

// Cleaning up redundant stuff.
remove-nodes:x:@json2lambda/*/items/*/*/links

// Returning entities to caller.
yield
   items:x:@json2lambda/*/items/*
   totalResults:x:@json2lambda/*/totalResults
