
/*
 * Slot for adding user to SendGrid mailing list version 2.
 *
 * This slots takes the type too and adds it as a custom field.
 */
slots.create:wl.sendgrid.add-to-list-2

   // Sanity checking invocation
   validators.mandatory:x:@.arguments/*/type
   validators.mandatory:x:@.arguments/*/name
   validators.mandatory:x:@.arguments/*/email
   validators.email:x:@.arguments/*/email

   /*
    * Checking if SendGrid is configured for "auto-add" at which point
    * we automatically add create a contact, and add it to the configured list.
    */
   if
      eq
         config.get:"magic:sendgrid:auto-add"
         .:yes
      .lambda

         // Invoking SendGrid to create recipient.
         .auth
         set-value:x:@.auth
            strings.concat
               .:"Bearer "
               config.get:"magic:sendgrid:api-key"
         config.get:"magic:sendgrid:default-list"
         set-name:x:./*/http.put/**/full_name
            config.get:"magic:sendgrid:full_name_id"
               .:e1_T
         set-name:x:./*/http.put/**/ml_type
            config.get:"magic:sendgrid:ml_type_id"
               .:e2_T
         unwrap:x:+/**
         http.put:"https://api.sendgrid.com/v3/marketing/contacts"
            convert:true
            headers
               Authorization:x:@.auth
               Content-Type:application/json
            payload
               list_ids
                  .:x:@config.get
               contacts
                  .
                     email:x:@.arguments/*/email
                     custom_fields
                        full_name:x:@.arguments/*/name
                        ml_type:x:@.arguments/*/type

         // Sanity checking invocation.
         if
            neq:x:@http.put
               .:int:202
            .lambda

               // Oops ...!!
               lambda2hyper:x:@http.put/*/content
               log.error:Could not add new contact to list
                  email:x:@.arguments/*/email
                  name:x:@.arguments/*/name
                  stack:x:@lambda2hyper

         else

            // Doing some basic logging
            log.info:Successfully added contact to SendGrid automation
               name:x:@.arguments/*/name
               email:x:@.arguments/*/email
