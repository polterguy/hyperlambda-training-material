
// Create a Hyperlambda action to execute a workflow file if a condition is true. The condition uses 'lhs', 'rhs', and 'comparison' (e.g., eq, neq, mt, mte, lt, lte). Include 'workflow' and optional 'arguments'. Validate and set comparison operator.
.arguments

   // Notice, this is action syntax
   lhs
      type:string
      mandatory:bool:true
   comparison
      type:enum
      mandatory:bool:true
      values
         .:eq
         .:neq
         .:mt
         .:mte
         .:lt
         .:lte
   rhs
      type:string
      mandatory:bool:true
   workflow
      type:workflow
      mandatory:bool:true
   arguments
      type:key-value
      mandatory:bool:false
.icon:settings

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/workflow
validators.mandatory:x:@.arguments/*/comparison

// Making sure we use correct comparison operator.
set-name:x:./*/if/*/comparison
   get-value:x:@.arguments/*/comparison

// Checking if [comparison] yields true.
if
   comparison
      get-value:x:@.arguments/*/lhs
      get-value:x:@.arguments/*/rhs
   .lambda

      // Parametrising [execute-file].
      add:x:./*/execute-file
         get-nodes:x:@.arguments/*/arguments/*

      // Executing file.
      execute-file:x:@.arguments/*/workflow

      // Parametrising [return] invocation.
      add:x:./*/return
         get-nodes:x:@execute-file/*

      // Returning result of execution to caller.
      return
