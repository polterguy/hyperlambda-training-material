
// Returns all extra fields for the specified [username].
.arguments
   username
      type:string
      mandatory:bool:true
.icon:supervisor_account

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/username

// Connecting to database.
data.connect:[generic|magic]

   // Verifying user exists.
   data.read
      table:users
      columns
         username
      where
         and
            username.eq:x:@.arguments/*/username

   // Verifying above invocation returned something.
   if
      not-exists:x:@data.read/*
      .lambda

         // Oops ...!!
         throw:No such user
            username:x:@.arguments/*/username

   // Selecting all extra fields associated with user.
   data.read
      table:users_extra
      values
         type
         value
      where
         and
            user.eq:x:@.arguments/*/username

   // Parametrizing [return] invocation below.
   for-each:x:@data.read/*
      set-name:x:+/+/*/*
         get-value:x:@.dp/#/*/type
      unwrap:x:+/*/*
      add:x:../*/return
         .
            name:x:@.dp/#/*/value

// Returning extra fields to caller.
return
