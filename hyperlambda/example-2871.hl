
// KPI file for how many activities was closed each day
.name:Closed activities
.description:Closed activities each day the last 30 days
.enabled:bool:true
.type:line
.lambda
   .now
   set-value:x:-
      math.subtract
         date.now
         time
            days:29
   data.connect:[generic|robo-crm]
      while
         lt:x:@.now
            date.now
         .lambda
            .max
            set-value:x:@.max
               math.add:x:@.now
                  time
                     days:1
            date.format:x:@.now
               format:yyyy-MM-dd
            date.format:x:@.max
               format:yyyy-MM-dd
            data.read
               table:activities
               columns
                  count(*)
                     as:name
               where
                  and
                     closed.mt:x:@date.format/@date.format
                     closed.lt:x:@date.format
            unwrap:x:+/*/*/*
            add:x:../*/return
               .
                  .
                     name:x:@date.format/@date.format
                     value:x:@data.read/*/*
            set-value:x:@.now
               math.add:x:@.now
                  time
                     days:1
   return
