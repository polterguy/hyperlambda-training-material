
// Write a Hyperlambda script to send a message using Twilio.
slots.create:magic.twilio.send-message

   // Sanity checking invocation.
   validators.mandatory:x:@.arguments/*/to
   validators.mandatory:x:@.arguments/*/from
   validators.mandatory:x:@.arguments/*/body
   validators.mandatory:x:@.arguments/*/twilio_account_id
   validators.mandatory:x:@.arguments/*/twilio_account_sid

   // Creating Basic auth header.
   .auth
   set-value:x:@.auth
      strings.concat
         get-value:x:@.arguments/*/twilio_account_id
         .:":"
         get-value:x:@.arguments/*/twilio_account_sid
   set-value:x:@.auth
      convert:x:@.auth
         type:bytes
   set-value:x:@.auth
      strings.concat
         .:"Basic "
         convert:x:@.auth
            type:base64

   // Removing Markdown images since Twilio doesn't support Markdown.
   .content
   set-value:x:@.content
      strings.regex-replace:x:@.arguments/*/body
         .:"!\\[.*\\]\\(.*\\)"
         .:
   set-value:x:@.content
      strings.trim:x:@.content
   .cont:bool:true
   while:x:@.cont
      if
         not
            strings.contains:x:@.content
               .:"\n\n\n"
         .lambda
            set-value:x:@.cont
               .:bool:false
      else
         set-value:x:@.content
            strings.replace:x:@.content
               .:"\n\n\n"
               .:"\n\n"

   // Fixing Markdown hyperlinks by removing brackets.
   set-value:x:@.content
      strings.replace:x:@.content
         .:[
         .:
   set-value:x:@.content
      strings.replace:x:@.content
         .:](
         .:" ("

   // Creating our URL to invoke Twilio API.
   .url
   set-value:x:@.url
      strings.concat
         .:"https://api.twilio.com/2010-04-01/Accounts/"
         get-value:x:@.arguments/*/twilio_account_id
         .:/Messages.json

   // Invoking Twilio.
   http.post:x:@.url
      convert:true
      headers
         Content-Type:application/x-www-form-urlencoded
         Authorization:x:@.auth
      payload
         Body:x:@.content
         From:x:@.arguments/*/from
         To:x:@.arguments/*/to

   // Making sure above invocation succeeded.
   if
      not
         and
            mte:x:@http.post
               .:int:200
            lt:x:@http.post
               .:int:300
      .lambda

         // Something went wrong while invoking Twilio
         lambda2hyper:x:@http.post/*/content
         log.error:Something went wrong while invoking Twilio
            response:x:@lambda2hyper
         throw:Something went wrong as we invoked Twilio, check your log for details

   // Logging success.
   log.info:Successfully sent message using Twilio
      body:x:@.arguments/*/body
      to:x:@.arguments/*/to
      from:x:@.arguments/*/from
      twilio_account_id:x:@.arguments/*/twilio_account_id
