
// Search for videos using YouTube with a mandatory query parameter.
.arguments

   // Mandatory query to search for.
   query:string
.type:public

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/query

// Retrieving SERP API's API key and making sure we've got one.
.api-key
set-value:x:@.api-key
   config.get:"magic:serp-api:api_key"
if
   or
      null:x:@.api-key
      eq:x:@.api-key
         .:
   .lambda

      // Oops ...!!
      throw:Could not find a SERP API key to use for fetching hotels.

// Creating our URL.
.url
set-value:x:@.url
   strings.concat
      .:"https://serpapi.com/search.json?engine=youtube&api_key="
      get-value:x:@.api-key
      .:&search_query=
      strings.url-encode:x:@.arguments/*/query

// Retrieving result.
http.get:x:@.url
   convert:true

// Sanity checking result of above invocation.
if
   not
      and
         mte:x:@http.get
            .:int:200
         lt:x:@http.get
            .:int:300
   .lambda

      // Oops ...!!
      lambda2hyper:x:../*
      log.info:x:-
      throw:Something went wrong while invoking SERP API

// Iterating through result, making sure we return the result to caller.
for-each:x:@http.get/*/content/*/video_results/*/[0,10]
   unwrap:x:+/*/*/*
   add:x:../*/return-nodes/*/result
      .
         .
            title:x:@.dp/#/*/title
            link:x:@.dp/#/*/link
            thumbnail:x:@.dp/#/*/thumbnail/*/rich
            description:x:@.dp/#/*/description
            length:x:@.dp/#/*/length

// Returning result to caller.
unwrap:x:+/*
return-nodes
   result
