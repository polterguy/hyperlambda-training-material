
// Compiles the specified C# file and loads the resulting assembly into the AppDomain
.arguments
   assembly-name
      type:string
      mandatory:bool:true
      default:your-assembly-name.dll
   recompile
      type:bool
      mandatory:bool:true
      default:bool:true
   references
      type:array
      mandatory:bool:true
      default
         .:netstandard
         .:System.Runtime
         .:System.ComponentModel
         .:System.Private.CoreLib
         .:magic.node
         .:magic.node.extensions
         .:magic.signals.contracts
   file
      type:string
      mandatory:bool:true
.icon:rocket_launch

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/file
validators.mandatory:x:@.arguments/*/references
validators.mandatory:x:@.arguments/*/assembly-name

/*
 * We only compile C# file if the assembly does not already exist in AppDomain,
 * unless caller explicitly specifies [recompile] as true.
 */
system.plugin.list
if
   or
      get-value:x:@.arguments/*/recompile
      not-exists:x:"@system.plugin.list/*/\"={@.arguments/*/assembly-name}\""
   .lambda

      // Parametrizing [system.compile] invocation.
      add:x:./*/system.compile
         get-nodes:x:@.arguments/*/references
         get-nodes:x:@.arguments/*/assembly-name

      // Loading file.
      load-file:x:@.arguments/*/file

      // Compiling C# code.
      system.compile
         code:x:@load-file

      // Loading assembly into AppDomain.
      system.plugin.load:x:@system.compile
