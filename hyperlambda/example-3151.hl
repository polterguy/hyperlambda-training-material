
// Creates a new object in NetSuite of the specified type
.arguments

   // Mandatory argument declaring the type of entity we want to create.
   type:string

   // Mandatory argument declaring the actual content of the new entity we want to create.
   object:*
.type:public

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/type
validators.mandatory:x:@.arguments/*/object
validators.mandatory:x:@.arguments/*/object/0

/*
 * Returns an access token for NetSuite.
 * 
 * Returns a JWT token that can be used to invoke NetSuite according to your configuration settings.
 * Notice, this will read account-id, certificate-id, and consumer-key from your configuration settings,
 * in addition to assume your private key exists in the file '/etc/netsuite/private-key.txt'.
 */
execute:magic.workflows.actions.execute
   name:netsuite-get-access-token
   filename:/modules/netsuite/workflows/actions/netsuite-get-access-token.hl
   arguments

// Creating our idempotency key to prevent replay attacks.
guid.new

// Creating invocation URL.
.url
set-value:x:@.url
   strings.concat
      .:"https://"
      config.get:"magic:netsuite:account-id"
      .:.suitetalk.api.netsuite.com/services/rest/record/v1
      .:/
      get-value:x:@.arguments/*/type

// Parametrizing our payload.
add:x:./*/http.post/*/payload
   get-nodes:x:@.arguments/*/object/*

// Invoking our HTTP GET endpoint, now correctly parametrised.
http.post:x:@.url
   token:x:--/execute/=netsuite-get-access-token/*/access-token
   headers
      X-NetSuite-Idempotency-Key:x:@guid.new
      Content-Type:application/json; charset=UTF-8
   payload

// Sanity checking above invocation.
if
   not
      and
         mte:x:@http.post
            .:int:200
         lt:x:@http.post
            .:int:300
   .lambda

      // Oops, error ...!!
      json2lambda:x:@http.post
      lambda2hyper:x:@json2lambda
      log.error:x:@lambda2hyper
      throw:Could not create entity in NetSuite, check your log for details.

// Returning success to caller.
return
   result:success
