
// Returns the Swagger and OpenAPI specification for the specified type from NetSuite
.arguments

   // Type of object the caller wants the OpenAPI spec for.
   type:string
.type:public

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/type

/*
 * Returns an access token for NetSuite.
 * 
 * Returns a JWT token that can be used to invoke NetSuite according to your configuration settings.
 * Notice, this will read account-id, certificate-id, and consumer-key from your configuration settings,
 * in addition to assume your private key exists in the file '/etc/netsuite/private-key.txt'.
 */
execute:magic.workflows.actions.execute
   name:netsuite-get-access-token
   filename:/modules/netsuite/workflows/actions/netsuite-get-access-token.hl
   arguments

// Creating our idempotency key to prevent replay attacks.
guid.new

// Creating invocation URL.
.url
set-value:x:@.url
   strings.concat
      .:"https://"
      config.get:"magic:netsuite:account-id"
      .:.suitetalk.api.netsuite.com/services/rest/record/v1/metadata-catalog
      .:/
      get-value:x:@.arguments/*/type

// Invoking our HTTP GET endpoint, now correctly parametrised.
http.get:x:@.url
   token:x:--/execute/=netsuite-get-access-token/*/access-token
   headers
      Accept:application/swagger+json
      X-NetSuite-Idempotency-Key:x:@guid.new

// Converts the result to a lambda object.
json2lambda:x:@http.get/*/content

// Sanity checking above invocation.
if
   not
      and
         mte:x:@http.get
            .:int:200
         lt:x:@http.get
            .:int:300
   .lambda

      // Oops, error ...!!
      lambda2hyper:x:@json2lambda
      log.error:x:@lambda2hyper
      throw:Could not get entity from NetSuite, check your log for details.

// Returning our entity to caller.
return:x:@json2lambda/*
