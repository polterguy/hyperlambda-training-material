
// Plugin slot returning cloudlets associated with user.
slots.create:aista.contacts.cloudlets

   // Making sure we've got a [meta] argument.
   if
      or
         not-exists:x:@.arguments/*/meta
         null:x:@.arguments/*/meta
         eq:x:@.arguments/*/meta
            .:
      .lambda
         return
            title:Cloudlets

   /*
    * Retrieving cloudlets associated with user.
    * Notice, username can be found in [meta] argument if invocation.
    */
   data.connect:[generic|hub]
      data.read
         table:cloudlets
         columns
            name
            plan
            cluster
            created
            id
         where
            and
               user.eq:x:@.arguments/*/meta

      // Iterating through each cloudlet returned above adding actions to each cloudlet.
      for-each:x:@data.read/*
         include:x:@.dp/#

            // Dynamically creating arguments to actions.
            .delete
            set-value:x:@.delete
               strings.concat
                  .:magic/modules/hub/admin/cloudlets?id=
                  get-value:x:@.dp/#/*/id
            .disable-update:bool:true
            if
               strings.starts-with:x:@.dp/#/*/plan
                  .:demo
               .lambda
                  set-value:x:@.disable-update
                     .:bool:false
            .plan:professional
            if
               strings.contains:x:@.dp/#/*/plan
                  .:angular
               .lambda
                  set-value:x:@.plan
                     .:professional-angular

            // Returning actions from [include].
            unwrap:x:+/**
            return
               actions
                  .
                     icon:delete
                     tooltip:Permanently deletes the cloudlet
                     url:x:@.delete
                     warning:This will permanently delete the cloudlet and all of its data. Please be 100% certain that this is what you want.
                     verb:delete
                  .
                     icon:account_balance
                     tooltip:Upgrade the cloudlet
                     url:magic/modules/hub/admin/upgrade-cloudlet
                     warning:This will upgrade the cloudlet to its professional equivalent. Are you sure you wish to continue?
                     verb:put
                     disabled:x:@.disable-update
                     payload
                        id:x:@.dp/#/*/id
                        plan:x:@.plan

         // Removing [id] to makes sure we don't return it to caller.
         remove-nodes:x:@.dp/#/*/id

      // Returning all cloudlets associated with user to caller, now with actions too.
      add:x:./*/return/*/items
         get-nodes:x:@data.read/*
      return
         title:Cloudlets
         items
