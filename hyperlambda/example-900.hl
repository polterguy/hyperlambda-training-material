
// Lists all indexes in some PostgreSQL table.
slots.create:magic.db.pgsql.indexes

   // Sanity checking invocation.
   validators.mandatory:x:@.arguments/*/database
   validators.mandatory:x:@.arguments/*/table

   // Opens up our connection
   .connection:[postgres]
   if
      and
         not
            eq
               get-value:x:@.arguments/*/connection-string
               .
         not
            eq
               get-value:x:@.arguments/*/connection-string
               .:
      .lambda
         set-value:x:@.connection
            strings.concat
               .:[
               get-value:x:@.arguments/*/connection-string
               .:|
               get-value:x:@.arguments/*/database
               .:]
   else
      set-value:x:@.connection
         strings.concat
            .:[generic|
            get-value:x:@.arguments/*/database
            .:]
   pgsql.connect:x:@.connection

      // Dynamically creating our SQL.
      strings.concat
         .:"\nselect\n    t.relname as table_name,\n    i.relname as index_name,\n    a.attname as column_name\nfrom\n    pg_class t,\n    pg_class i,\n    pg_index ix,\n    pg_attribute a\nwhere\n    t.oid = ix.indrelid\n    and i.oid = ix.indexrelid\n    and a.attrelid = t.oid\n    and a.attnum = ANY(ix.indkey)\n    and t.relkind = 'r'\n    and t.relname = '"
         get-value:x:@.arguments/*/table
         .:"'"
      pgsql.select:x:@strings.concat

      /*
       * Creating our return value, by projecting it into
       * a generic form, that does not rely upon MySQL's syntactic
       * details.
       */
      for-each:x:@pgsql.select/*
         unwrap:x:+/*/*/*
         add:x:@pgsql.connect/*/return-nodes
            .
               .
                  name:x:@.dp/#/*/index_name
                  columns
         unwrap:x:+/*/*
         add:x:@pgsql.connect/*/return-nodes/0/-/*/columns
            .
               .:x:@.dp/#/*/column_name
      return-nodes
