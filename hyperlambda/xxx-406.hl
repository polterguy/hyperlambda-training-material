
// Write a Hyperlambda script to evaluate the given SQL for a specified database and return the result to the caller.
.arguments
   databaseType:string
   database:string
   sql:string
   safeMode:bool
   batch:bool

// Ensures user is authorized to access endpoint.
auth.ticket.verify:root

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/databaseType
validators.enum:x:@.arguments/*/databaseType
   .:mysql
   .:pgsql
   .:mssql
   .:sqlite

// Creating our database connection.
data.connect:x:@.arguments/*/database
   database-type:x:@.arguments/*/databaseType

   /*
    * Checking if we're supposed to execute SQL in safe mode, only
    * returning the first 200 records.
    */
   if
      and
         get-value:x:@.arguments/*/safeMode
         not
            get-value:x:@.arguments/*/batch
      .lambda
         add:x:@data.connect/**/data.select
            .
               max:200

   /*
    * Checking if we're supposed to execute SQL as an MS SQL 'batch' operation,
    * which implies respecting 'go' keywords, splitting SQL by each 'go' keyword.
    */
   if
      and
         eq
            get-value:x:@.arguments/*/databaseType
            .:mssql
         get-value:x:@.arguments/*/batch
      .lambda
         set-name:x:@data.connect/**/data.select
            .:mssql.execute-batch
   else
      add:x:@data.connect/**/data.select
         .
            database-type:x:@.arguments/*/databaseType

   /*
    * Executing SQL and returning results to caller, making sure we're
    * able to return exceptions to caller.
    */
   try
      data.select:x:@.arguments/*/sql
         multiple-result-sets:true

      // Returning result of invocation to caller.
      if
         exists:x:@data.select/*/*
         .lambda
            return-nodes:x:@data.select/*
   .catch

      // Notice, throwing exceptions as public now.
      throw:x:@.arguments/*/message
         public:bool:true
         status:int:401
