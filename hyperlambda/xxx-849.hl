
// Create a function that returns all cloudlets belonging to the currently authenticated user.
.description:Returns all cloudlets belonging to the currently authenticated user
.type:crud-read

// Ensuring user is authorised to invoke endpoint.
auth.ticket.verify:root, guest, impersonated

/*
 * Retrieving currently authenticated user to prevent a user
 * from retrieving cloudlets not belonging to him/her.
 */
auth.ticket.get

// Opening up our database connection.
data.connect:[generic|hub]

   // Reading data from database.
   data.read
      table:cloudlets
         join:plans
            type:inner
            on
               and
                  cloudlets.plan.eq:plans.name
         join:clusters
            type:inner
            on
               and
                  cloudlets.cluster.eq:clusters.id
      columns
         cloudlets.id
         cloudlets.created
         cloudlets.name
         cloudlets.plan
         cloudlets.cluster
         cloudlets.destruction_date
         cloudlets.enabled
         cloudlets.source_cloudlet
         plans.price
         plans.storage
         plans.cpu
         plans.memory
         plans.frontend
         plans.max_upload
         plans.bandwidth
         plans.max_timeout
         clusters.country
         clusters.name
            as:node_name
         plans.description
      where
         and
            user.eq:x:@auth.ticket.get

   // Returning result of above read invocation to caller.
   return-nodes:x:@data.read/*
